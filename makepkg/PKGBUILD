# Maintainer: David Wildasin <https://github.com/dragonmaus>
pkgname=aurutils-artix-git
pkgver=2.2.0.r16.gecdb75b
pkgrel=1
pkgdesc='helper tools for the arch user repository (artix fork)'
url='https://github.com/dragonmaus/aurutils-artix'
arch=('any')
license=('custom:ISC')
source=('git+https://github.com/dragonmaus/aurutils-artix')
sha256sums=('SKIP')
conflicts=('aurutils' 'aurutils-git')
provides=("aurutils=${pkgver%%.r*}")
depends=('git' 'jq' 'expac' 'diffstat' 'pacutils' 'parallel' 'wget')
makedepends=('git')
optdepends=('bash-completion: bash completion'
            'artools-pkg: aur-chroot'
            'vifm: build file interaction'
            'xdelta3: generate delta files')

pkgver() {
    cd aurutils-artix
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd aurutils-artix
    make DESTDIR="$pkgdir"
}

package() {
    cd aurutils-artix
    make DESTDIR="$pkgdir" install
}
